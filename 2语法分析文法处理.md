# 文法处理

```c++
编译单元 CompUnit → {Decl} {FuncDef} MainFuncDef 
声明 Decl → ConstDecl | VarDecl 
常量声明 ConstDecl → 'const' BType ConstDef { ',' ConstDef } ';' 
基本类型 BType → 'int' 
常数定义 ConstDef → Ident { '[' ConstExp ']' } '=' ConstInitVal 
常量初值 ConstInitVal → ConstExp | '{' [ ConstInitVal { ',' ConstInitVal } ] '}' 
变量声明 VarDecl → BType VarDef { ',' VarDef } ';' 
变量定义 VarDef → Ident { '[' ConstExp ']' } 
				| Ident { '[' ConstExp ']' } '=' InitVal 
变量初值 InitVal → Exp | '{' [ InitVal { ',' InitVal } ] '}'
函数定义 FuncDef → FuncType Ident '(' [FuncFParams] ')' Block 
主函数定义 MainFuncDef → 'int' 'main' '(' ')' Block 
函数类型 FuncType → 'void' | 'int' 
函数形参表 FuncFParams → FuncFParam { ',' FuncFParam } 
函数形参 FuncFParam → BType Ident ['[' ']' { '[' ConstExp ']' }] 
语句块 Block → '{' { BlockItem } '}' 
语句块项 BlockItem → Decl | Stmt 
标识符 identifier → identifier-nondigit 
 				| identifier identifier-nondigit
语句 Stmt → LVal '=' Exp ';' 
    		| [Exp] ';'
			| Block 
			| 'if' '(' Cond ')' Stmt [ 'else' Stmt ] 
			| 'while' '(' Cond ')' Stmt 
			| 'break' ';' 
			| 'continue' ';' 	
			| 'return' [Exp] ';' 
			| LVal '=' 'getint''('')'';' 
			| 'printf''('FormatString{','Exp}')'';' 
Stmt → (LVal '=')('getint''('')'';' | Exp ';') 
    | Block 
    | 'if' '(' Cond ')' Stmt [ 'else' Stmt ] 
    | 'while' '(' Cond ')' Stmt 
    | 'break' ';' 
    | 'continue' ';' 
    | 'return' [Exp] ';' 
    | 'printf''('FormatString{','Exp}')'';' 
表达式 Exp → AddExp 注：SysY 表达式是int 型表达式 
条件表达式 Cond → LOrExp 
左值表达式 LVal → Ident {'[' Exp ']'} 
基本表达式 PrimaryExp → '(' Exp ')' | LVal | Number 
数值 Number → IntConst 
一元表达式 UnaryExp → PrimaryExp | Ident '(' [FuncRParams] ')' 
					| UnaryOp UnaryExp 
单目运算符 UnaryOp → '+' | '−' | '!' 注：'!'仅出现在条件表达式中 
函数实参表 FuncRParams → Exp { ',' Exp } 
乘除模表达式 MulExp → UnaryExp | MulExp ('*' | '/' | '%') UnaryExp 
MulExp → UnaryExp {('*' | '/' | '%') UnaryExp}
加减表达式 AddExp → MulExp | AddExp ('+' | '−') MulExp 
AddExp → MulExp {('+' | '-') MulExp}
关系表达式 RelExp → AddExp | RelExp ('<' | '>' | '<=' | '>=') AddExp 
RelExp → AddExp {('<' | '>' | '<=' | '>=') AddExp}
相等性表达式 EqExp → RelExp | EqExp ('==' | '!=') RelExp 
EqExp → RelExp {('==' | '!=') RelExp}
逻辑与表达式 LAndExp → EqExp | LAndExp '&&' EqExp 
LAndExp → EqExp {'&&' EqExp}
逻辑或表达式 LOrExp → LAndExp | LOrExp '||' LAndExp 
LOrExp → LAndExp {'||' LAndExp}
常量表达式 ConstExp → AddExp 注：使用的Ident 必须是常量 
```



```
CompUnit -> {Decl} {FuncDef} MainFuncDef

```



## 求解FIRST、FOLLOW集合

**对于大括号、中括号是否展开的问题，应该把大括号、中括号看作一个整体来求FOLLOW。大括号or中括号中间的非终结符，不能决定是否展开**

### `CompUnit`

**FIRST**

```c
FIRST(Decl) && FIRST(FuncDef) && FIRST(MainFuncDef)
'int', 'const', 'void'
```

**FOLLOW**

```c
eof
```



### `Decl`

**FIRST**

```c
FIRST(ConstDecl) && FIRST(VarDecl)
'int', 'const'
```

**FOLLOW**

```c
FIRST(Decl), FIRST(FuncDef), FIRST(MainFuncDef), FOLLOW(BlockItem)
'int', 'const', 'void', '}'
```



### `ConstDecl`

**FIRST**

```c
'const'
```

**FOLLOW**

```c
FOLLOW(Decl)
'int', 'const', 'void', '}'
```



### `BType`

**FIRST**

```c
'int'
```

**FOLLOW**

```c
FIRST(ConstDef), FIRST(VarDef), FIRST(Ident)
',', ';', Ident
```



### `ConstDef`

**FIRST**

```c
Ident
```

**FOLLOW**

```c
',', ';'
```



### `ConstInitVal`

**FIRST**

```c
FIRST(ConstExp), '{'
'+', '-', '!', ident, '(', IntConst, '{'
```

**FOLLOW**

```c
FOLLOW(ConstDef), ',', '}'
',', ';', '}'
```



### `VarDecl`

**FIRST**

```c
FIRST(BType)
'int'
```

**FOLLOW**

```c
FOLLOW(Decl)
'int', 'const', 'void', '}'
```



### ----`VarDef`

**FIRST**

```c
文法中: VarDef -> Ident {'[' ConstExp ']'}
				|Ident {'[' ConstExp ']'} '=' ConstInitVal
FIRST
Ident
```

**FOLLOW**

```c
',', ';'
```



### `InitVal`

**FIRST**

```c
FIRST(Exp), '{'
'+', '-', '!', ident, '(', IntConst, '{'
```

**FOLLOW**

```c
FOLLOW(VarDef), ',', '}'
',', ';', '}'
```



### `FuncDef`

**FIRST**

```c
FIRST(FuncType)
'void', 'int'
```

**FOLLOW**

```c
FIRST(MainFuncDef)
'int'
```



### `MainFuncDef`

**FIRST**

```c
'int'c
```

**FOLLOW**

```c
FOLLOW(CompUnit)
eof
```



### `FuncType`

**FIRST**

```c
'void', 'int'
```

**FOLLOW**

```c
Ident
```



### `FuncFParams`

**FIRST**

```c
文法中：FuncFParams → FuncFParam { ',' FuncFParam }
FIRST(FuncFParam)
int 
```

**FOLLOW**

```c
')'
```



### `FuncFParam`

**FIRST**

```c
FIRST(BType)
-- int 
```

**FOLLOW**

```c
FOLLOW(FuncFParams), ','
')', ','
```



### `Block`

**FIRST**

```c
'{'
```

**FOLLOW**

```c
FOLLOW(FuncDef), FOLLOW(MainFuncDef), FOLLOW(Stmt)
'int', eof, '}', 'else'
```



### `BlockItem`

**FIRST**

```c
FIRST(Decl), FIRST(Stmt)
Ident, '+', '-', '!', '(', IntConst, ';', '{', 'if', 'while', 'break', 'return', 'printf', 'int', 'const', 'continue'
```

**FOLLOW**

```c
'}'
```



### `Stmt`

**FIRST**

```c
FIRST(LVal){如果是LVal，则之后需要判断是否是getint}, FIRST(Exp), ';', FIRST(BLOCK), 'if', 'while', 'break', 'return', 'printf', 'continue'
Ident, '+', '-', '!', '(', IntConst, ';', '{', 'if', 'while', 'break', 'return', 'printf'
```

**FOLLOW**

```c
FOLLOW(BolckItem), 'else', FOLLOW(Stmt)
'}', 'else'
```



### `Exp`

**FIRST**

```c
FIRST(AddExp)
'+', '-', '!', ident, '(', IntConst
```

**FOLLOW**

```c
FOLLOW(InitVal), ';', ')', ']', FOLLOW(FuncRParams)
',', ';', '}', ')', ']'
```



### `Cond`

**FIRST**

```c
FIRST(LOrExp)
'+', '-', '!', ident, '(', IntConst
```

**FOLLOW**

```c
')'
```



### `LVal`

**FIRST**

```c
Ident
```

**FOLLOW**

```c
'=', FOLLOW(PrimaryExp)
'=', ')', '||', '&&', '==', '!=', '<', '>', '<=', '>=', ',', ';', '}', ']', '+', '-', '*', '/', '%'
```



### `PrimaryExp`

**FIRST**

```c
'(', LVal, Number
'(', IntConst, Ident
```

**FOLLOW**

```c
FOLLOW(UnaryExp)
')', '||', '&&', '==', '!=', '<', '>', '<=', '>=', ',', ';', '}', ']', '+', '-', '*', '/', '%'
```



### `Number`

**FIRST**

```c
IntConst
```

**FOLLOW**

```c
FOLLOW(PrimaryExp)
')', '||', '&&', '==', '!=', '<', '>', '<=', '>=', ',', ';', '}', ']', '+', '-', '*', '/', '%'
```



### `UnaryExp`

**FIRST**

```c
FIRST(PrimaryExp), Ident, FIRST(UnaryOp)
'+', '-', '!', ident, '(', IntConst
```

**FOLLOW**

```c
FOLLOW(UnaryExp), FOLLOW(MulExp), '*', '/', '%'
')', '||', '&&', '==', '!=', '<', '>', '<=', '>=', ',', ';', '}', ']', '+', '-', '*', '/', '%'
```



### `UnaryOp`

**FIRST**

```c
'+', '-', '!'
```

**FOLLOW**

```c
FIRST(UnaryExp)
'+', '-', '!', ident, '(', IntConst
```



### `FuncRParams`

**FIRST**

```c
FIRST(Exp)
'+', '-', '!', ident, '(', IntConst
```

**FOLLOW**

```c
')'
```



### `MulExp`

**FIRST**

```c
FIRST(UnaryExp)
'+', '-', '!', ident, '(', IntConst
```

**FOLLOW**

```c
FOLLOW(AddExp), '+', '-'
')', '||', '&&', '==', '!=', '<', '>', '<=', '>=', ',', ';', '}', ']', '+', '-'
```



### `AddExp`

**FIRST**

```c
AddExp → MulExp {('+' | '-') MulExp}
FIRST(MulExp)
'+', '-', '!', ident, '(', IntConst
```

**FOLLOW**

```c
FOLLOW(RelExp), '<', '>', '<=', '>=', FOLLOW(Exp), FOLLOW(ConstExp)
')', '||', '&&', '==', '!=', '<', '>', '<=', '>=', ',', ';', '}', ']'
```



### `RelExp`

**FIRST**

```c
RelExp → AddExp {('<' | '>' | '<=' | '>=') AddExp}
FIRST(AddExp)
'+', '-', '!', ident, '(', IntConst
```

**FOLLOW**

```c
FOLLOW(EqExp), '==', '!='
')', '||', '&&', '==', '!='
```



### `EqExp`

**FIRST**

```c
EqExp → RelExp {('==' | '!=') RelExp}
FIRST(RelExp)
'+', '-', '!', ident, '(', IntConst
```

**FOLLOW**

```c
FOLLOW(LAndExp), '&&'
')', '||', '&&'
```



### `LAndExp`

**FIRST**

```c
LAndExp → EqExp {'&&' EqExp}
FIRST(EqExp)
'+', '-', '!', ident, '(', IntConst
```

**FOLLOW**

```c
FOLLOW(LOrExp), '||'
')', '||'
```



### `LOrExp`

**FIRST**

```c
LOrExp → LAndExp {'||' LAndExp}
FIRST(LAndExp)
'+', '-', '!', ident, '(', IntConst
```

**FOLLOW**

```c
FOLLOW(Cond)
')'
```



### `ConstExp`

**FIRST**

```c
FIRST(AddExp)
'+', '-', '!', ident, '(', IntConst
```

**FOLLOW**

```c
']', FOLLOW(ConstInitVal)
',', ';', '}', ']'
```

